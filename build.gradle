
def mcVersion = "1.7"
def versionMajor = 3
def versionMinor = 0
def versionRev = 13

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge' // Add forge dependency
apply plugin: 'idea'

sourceSets.main {
    java {
        srcDirs = ['src/main/java', 'dependencies/main/java']
    }
    resources {
        srcDirs = ['src/main/resources', 'dependencies/main/resources']
    }
}
	
version = "${mcVersion}-${versionMajor}.${versionMinor}.${versionRev}-GTNH" // Set the version number
group= "micdoodle8.mods.galacticraft"
archivesBaseName = "GalacticraftCore"
	
minecraft {
    version = "1.7.10-10.13.4.1448-1.7.10" // Forge version
    runDir = "run" // This is where forge will download Minecraft assets
}

configurations {
	provided
	embedded
	compile.extendsFrom provided, embedded
}

repositories {
    maven {
        name 'Forge'
        url 'http://files.minecraftforge.net/maven'
    }
    maven {
        //Everything
        name = 'curseforge'
        url = 'https://minecraft.curseforge.com/api/maven/'
    }
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven"
    }
    maven {
        name = "ic2, forestry"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        name = "gt"
        url = "https://gregtech.overminddl1.com/"
    }
    maven {
        url = "https://mod-buildcraft.com/releases/"
    }
    maven { // TConstruct
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    ivy {
        name 'gtnh_download_source_stupid_underscore_typo'
        artifactPattern "http://downloads.gtnewhorizons.com/Mods_for_Jenkins/[module]_[revision].[ext]"
    }
    ivy {
        name 'gtnh_download_source'
        artifactPattern "http://downloads.gtnewhorizons.com/Mods_for_Jenkins/[module]-[revision].[ext]"
    }
}

dependencies {
	compile files("libs/MicdoodleCore-1.7.10-3.0.13-GTNH-dev.jar")

    compile "player-api:PlayerAPI:1.7.10:1.4"
    compile "render-player-api:RenderPlayerAPI:1.7.10:1.4"
    compile "smart-render:SmartRender:1.7.10:2.1" // SmartRender-1.7.10-2.1
    
    compile "tconstruct:TConstruct:1.7.10-1.8.4.build951:deobf"
    
    compileOnly "biomes-o-plenty:BiomesOPlenty-1.7.10:2.1.0.2308:universal"
    compileOnly "com.mod-buildcraft:buildcraft:7.1.23:api"

    compileOnly "codechicken:CodeChickenCore:1.7.10-1.0.7.46:dev"
    compileOnly "codechicken:CodeChickenLib:1.7.10-1.1.3.138:dev" 
    compileOnly "notenoughitems:NotEnoughItems:1.7.10-1.0.5.111:dev"

    provided ("appeng:appliedenergistics2:rv3-beta-22:dev") {
        exclude module: '*'
    }

    compileOnly "net.industrial-craft:industrialcraft-2:2.2.790-experimental:dev"
}
					
// Keep a list of core files/directories for easy jar packing
def apiFiles = ['micdoodle8/mods/galacticraft/api/**']
					
// Keep a list of core files/directories for easy jar packing
ext.coreFiles = ['micdoodle8/mods/galacticraft/api/**',
					'micdoodle8/mods/galacticraft/core/**',
					'tconstruct/**',
					'assets/minecraft/**',
					'assets/galacticraftmoon/**',
					'assets/galacticraftcore/**',
					'**/dependenciesGC.info']
					
// Keep a list of planets addon files/directories for easy jar packing
def planetsFiles = ['micdoodle8/mods/galacticraft/planets/**',
					'assets/galacticraftmars/**',
					'assets/galacticraftasteroids/**']

jar {
	exclude planetsFiles
}

task planetJar(type: Jar) {
	from sourceSets.main.output
	baseName = "Galacticraft-Planets"
	include planetsFiles
}

task devJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'dev'
    exclude planetsFiles
}

task planetDevJar(type: Jar) {
    from sourceSets.main.output
    baseName = "Galacticraft-Planets"
    classifier = 'dev'
    include planetsFiles
}


task apiJar(type: Jar) {
    from sourceSets.main.allSource
	// Jar will always be updated
	outputs.upToDateWhen {false}
	// Set the jar's base name
	baseName = "Galacticraft"
    classifier = 'api'
    
    include apiFiles
}

// Tell the artifact system about our extra jars
artifacts {
	archives planetJar, devJar, planetDevJar, apiJar
}
